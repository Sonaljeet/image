Sub DownloadAndSave(ByVal fileURL As String, ByVal savePath As String, ByVal detectExtension As Boolean)
    Dim http As Object, stream As Object
    Dim contentType As String, fileExt As String

    Set http = CreateObject("MSXML2.ServerXMLHTTP")
    http.Open "GET", fileURL, False
    
    On Error GoTo ErrHandler
    http.Send

    If http.Status = 200 Then
        If detectExtension Then
            contentType = LCase(http.getResponseHeader("Content-Type"))
            Select Case contentType
                Case "application/pdf": fileExt = ".pdf"
                Case "application/vnd.openxmlformats-officedocument.wordprocessingml.document": fileExt = ".docx"
                Case "application/msword": fileExt = ".doc"
                Case Else: fileExt = ".bin"
            End Select
            savePath = savePath & fileExt
        End If

        Set stream = CreateObject("ADODB.Stream")
        stream.Type = 1 'binary
        stream.Open
        stream.Write http.responseBody
        stream.SaveToFile savePath, 2 'overwrite
        stream.Close
    Else
        MsgBox "Failed to download: " & fileURL & vbCrLf & "Status: " & http.Status
    End If
    Exit Sub

ErrHandler:
    MsgBox "Access denied or request failed for URL: " & fileURL, vbCritical
End Sub
